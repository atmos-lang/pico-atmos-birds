^["pico.ceu"]

pico_state_set_title(`:pointer "Birds - 07 (iterator)"`)
pico_state_set_size(640,480)
pico_state_set_size_pixel(1,1)
pico_state_set_grid(false)
pico_state_set_color_clear(0,0,0,255)

var win = pico_state_get_size()

task Bird (y,speed) {
    set pub = [(-win.0)/2, y, 100, 90]
    var sin = 0
    watching evt.type == :collided {
        watching pub.0>320 {
            par {
                every evt.type == :frame {
                    var v = evt.dt * speed
                    set pub.0 = pub.0 + (v/1000)
                    set pub.1 = y + ((speed/5) * `:number sin($sin.Number)`)
                    set sin = sin + (3.14*v/100000)
                }
            } with {
                every evt.type == :draw {
                    if `:number ((int)(($sin.Number+(3.14/2))/3.14))` % 2 == 0 {
                        pico_output_draw_image(pub.0,pub.1, `:pointer "res/clumsy_up.png"`);
                    } else {
                        pico_output_draw_image(pub.0,pub.1, `:pointer "res/clumsy_dn.png"`);
                    }
                }
            }
        }
    }
}

spawn {
    var birds = coroutines(5)
    par {
        every 500ms {
            (spawn in birds, Bird(y,s)) where {
                var y = (`:number rand()` % 480) - 240
                var s = 100 + (`:number rand()` % 100)
            }
        }
    } with {
        every evt.type == :frame {
            while b1 in birds {
                while b2 in birds {
                    if b1 /= b2 {
                        if pico_rect_vs_rect?(b1.pub,b2.pub) {
                            broadcast in b1, @[ (:type,:collided) ]
                            broadcast in b2, @[ (:type,:collided) ]
                        }
                    }
                }
            }
        }
    }
}

pico_loop ()

