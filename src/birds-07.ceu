^"prelude.ceu"
^"int.ceu"
^"float.ceu"
^"pico.ceu"

type Event += <
    Collided = ()
>

var pi: _float = _(3.14)

output Pico.Set.Title _("Birds - 07 (iterator)")
output Pico.Set.Size [_640,_480]
output Pico.Set.Zoom [_100,_100]
output Pico.Set.Grid _0
output Pico.Set.Color.Clear [_0,_0,_0,_255]
output Pico.Clear
output Pico.Set.Auto _0

var win: Size
output Pico.Get.Size.Window [/win]

type Bird = task [_int,_int] -> Rect -> ()

task bird: Bird {       -- arg = [y,speed]
    var speed = arg.2
    set pub = [[x,arg.1], [_100,_90]]
        where {
            var x: _int = div [umn win.1, _2]
        }
    var sin: _float = _0

    watching evt?Collided {
        paror {
            var xx: _float = pub.1.1
            var yy: _float = pub.1.2
            every evt?Frame {
                var xspeed = speed
                var v: _float = mul [evt!Frame,speed]
                set xx = fadd [xx, fdiv [v,_1000]]
                set pub.1 = [x,y]
                    where {
                        var x: _int = f2int xx
                        if gt [x,_320] {
                            break
                        }
                        var y: _int = fadd [yy, fmul [int2f div [xspeed,_5],_(sin($sin))]]
                    }
                set sin = fadd [sin, _($pi*$v/100000)]
            }
        } with {
            every evt?Draw {
                if _( ((int)(($sin+$pi/2)/$pi)) % 2 == 0 ) {
                    output Pico.Draw.Image [pub.1, _("res/clumsy_up.png")]
                } else {
                    output Pico.Draw.Image [pub.1, _("res/clumsy_dn.png")]
                }
            }
        }
    }
}

spawn {
    var birds: active {5} Bird
    par {
        every 500ms {
            spawn bird [y,s] in birds
                where {
                    var y = sub [rem [_(rand()),_480], _240]
                    var s = add [_100, rem [_(rand()),_100]]
                }
        }
    } with {
        every evt?Frame {
            var b1: active Bird
            loop b1 in birds {
                var b2: active Bird
                loop b2 in birds {
                    if _($b1 != $b2) {
                        if isRectVsRect [b1.pub,b2.pub] {
                            emit b1 Event.Collided
                            emit b2 Event.Collided
                        }
                    }
                }
            }
        }
    }
}

call pico_loop ()
