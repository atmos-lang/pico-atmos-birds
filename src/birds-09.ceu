^["pico.ceu"]

pico_state_set_title("Birds - 09 (blink)")
pico_state_set_size(640,480)
pico_state_set_size_pixel(1,1)
pico_state_set_grid(false)
pico_state_set_color_clear(0,0,0,255)

var win = pico_state_get_size()

task Bird (y,speed) {
    set pub = [(-win.0)/2, y, 100, 90]
    var sin = 0
    awaiting pub.0 > 320 {
        awaiting :collided {
            par {
                every :frame {
                    var v = evt.0 * speed
                    set pub.0 = pub.0 + (v/1000)
                    set pub.1 = y + ((speed/5) * `:number sin($sin.Number)`)
                    set sin = sin + (3.14*v/100000)
                }
            } with {
                every :draw {
                    if `:number ((int)(($sin.Number+(3.14/2))/3.14))` % 2 == 0 {
                        pico_output_draw_image(pub.0,pub.1, "res/clumsy_up.png");
                    } else {
                        pico_output_draw_image(pub.0,pub.1, "res/clumsy_dn.png");
                    }
                }
            }
        }
        awaiting pub.1 < -200 {
            par {
                every :frame {
                    set pub.1 = pub.1 - (evt.0 * 0.5)
                }
            } with {
                every :draw {
                    pico_output_draw_image(pub.0,pub.1, "res/clumsy_dn.png");
                }
            }
        }
        awaiting 1s {
            par {
                while true {
                    broadcast in :task, tags([], :hide, true)
                    await 100ms
                    broadcast in :task, tags([], :show, true)
                    await 100ms
                }
            } with {
                toggle :hide -> :show {
                    every :draw {
                        pico_output_draw_image(pub.0,pub.1, "res/clumsy_dn.png");
                    }
                }
            }
        }
    }
}

spawn {
    var birds = coroutines(5)
    par {
        every 500ms {
            (spawn in birds, Bird(y,s)) where {
                var y = (`:number rand()` % 480) - 240
                var s = 100 + (`:number rand()` % 100)
            }
        }
    } with {
        every :frame {
            while in birds, b1 {
                while in birds, b2 {
                    if (b1.pub/=nil) and (b2.pub/=nil) and (b1/=b2) {
                        var r1 = [b1.pub.0,b1.pub.1,b1.pub.2,b1.pub.3]
                        var r2 = [b2.pub.0,b2.pub.1,b2.pub.2,b2.pub.3]
                        if pico_rect_vs_rect?(r1,r2) {
                            broadcast in b1, tags([], :collided, true)
                            broadcast in b2, tags([], :collided, true)
                        }
                    }
                }
            }
        }
    }
}

pico_loop ()
