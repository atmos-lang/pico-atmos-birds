^"int.ceu"
^"float.ceu"
^"pico.ceu"

var pi: _float = _(3.14)

output pico Pico.Output.Set.Title _("Birds - 09 (pause)")
output pico Pico.Output.Set.Size [_641,_481]
output pico Pico.Output.Set.Zoom [_100,_100]
output pico Pico.Output.Set.Grid _0
output pico Pico.Output.Set.Color.Clear [_0,_0,_0,_255]
output pico Pico.Output.Clear
output pico Pico.Output.Set.Auto _0

var win: Size
output pico Pico.Output.Get.Size /win

type Bird = task [_int,_int] -> Rect -> ()

var bird = Bird { @SELF      -- arg = [y,speed]
    var speed = arg.2
    set pub = [[x,arg.1], [_50,_45]]
        where {
            var x: _int = div [umn win.1, _2]
        }
    var sin: _float = _0

    paror {
        var xx: _float = pub.1.1
        var yy: _float = pub.1.2
        var out: <(),()> = <.1>
        watching evt?Collided {
            every evt?Frame {
                var xspeed = speed
                var v: _float = mul [evt!Frame,speed]
                set xx = fadd [xx, fdiv [v,_1000]]
                set pub.1 = [x,y]
                    where {
                        var x: _int = f2int xx
                        if gt [x,_320] {
                            set out = <.2>
                            break
                        }
                        var y: _int = fadd [yy, fmul [int2f div [xspeed,_5],_(sin($sin))]]
                    }
                set sin = fadd [sin, _($pi*$v/100000)]
            }
        }
        if out?1 {
            every evt?Frame {
                set yy = fsub[yy, fmul [int2f evt!Frame, _(0.5):_float]]
                set pub.1.2 = yy
                if lt [pub.1.2, umn _200] {
                    break
                }             
            }
            watching 1s {
                loop {
                    emit @SELF Event.Hide _1
                    await 100ms
                    emit @SELF Event.Hide _0
                    await 100ms
                }
            }
        }
    } with {
        pauseif evt?Hide {
            every evt?Draw {
                if _( ((int)(($sin+$pi/2)/$pi)) % 2 == 0 ) {
                    output pico Pico.Output.Draw.Image [pub.1, _("res/clumsy_up.png")]
                } else {
                    output pico Pico.Output.Draw.Image [pub.1, _("res/clumsy_dn.png")]
                }
            }
        }
    }
}

spawn { @PAUSE
    par {
        pauseif evt?Pause {
            var birds: active {5} Bird
            par {
                every 500ms {
                    spawn bird [y,s] in birds
                        where {
                            var y = sub [rem [_(rand()),_480], _240]
                            var s = add [_100, rem [_(rand()),_100]]
                        }
                }
            } with {
                every evt?Frame {
                    var b1: active Bird
                    loop b1 in birds {
                        var b2: active Bird
                        loop b2 in birds {
                            if _($b1 != $b2) {
                                if isRectVsRect [b1.pub,b2.pub] {
                                    emit b1 Event.Collided
                                    emit b2 Event.Collided
                                }
                            }
                        }
                    }
                }
            }
        }
    } with {
        loop {
            await evt?Key?Down
            emit @PAUSE Event.Pause _1
            spawn {
                every evt?Draw {
                    output pico Pico.Output.Draw.Image [[_0,_0], _("res/pause.png")]
                }
            }
            await evt?Key?Down
            emit @PAUSE Event.Pause _0
        }
    }
}

call pico_loop ()
