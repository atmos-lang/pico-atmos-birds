^["@/pico/pico.ceu"]

val math-sin = func (v) {
    `:number sin($v.Number)`
}
val math-floor = func (v) {
    `:number floor($v.Number)`
}
val random-seed = func (n) {
    if n {
        `srand($n.Number);`
    } else {
        `srand(time(NULL));`
    }
}
val random-next = func () {
    `:number rand()`
}

pico-state-set-title        ("Birds - 11 (tracking)")
pico-state-set-size         ([640,480])
pico-state-set-size-pixel   ([1,1])
pico-state-set-grid         (false)
pico-state-set-color-clear  ([0,0,0,255])
pico-state-set-color-draw   ([255,255,255,255])

random-seed()

val WIN :XY = pico-state-get-size()

val Delay = task (ms) {
    var tot = ms
    xloop {
        xbreak if yield(nil) { as it =>
            if is?(it,:frame) {
                set tot = tot - it[0]
                tot <= 0
            } else {
                false
            }
        }
    }
}

data :Bird = [alive,rect:Rect]

val Bird = task (t) :Bird {
    val y = t[0]
    val speed = t[1]
    set pub() = [true, [[(-WIN.x)/2,y], [100,90]]]
    var ang = 0
    val Y =
        spawn (task () :void {
            xloop {
                xbreak if yield(nil) { as it :Pico.Frame =>
                    if is?(it, :Pico.Frame) {
                        val v = it.ms * speed
                        set pub().rect.pos.x = pub().rect.pos.x + (v/1000)
                        set pub().rect.pos.y = y + ((speed/5) * math-sin(ang))
                        set ang = ang + ((3.14*v)/100000)
                        false
                    } else {
                        if is?(it, :Pico.Draw) {
                            if (math-floor(((ang+(3.14/2))/3.14)) % 2) == 0 {
                                pico-output-draw-image(pub().rect.pos, "res/clumsy_up.png");
                            } else {
                                pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png");
                            }
                            false
                        } else {
                            (it == :collided)
                        }
                    }
                }
            }

            set pub().alive = false

            xloop {
                xbreak if yield(nil) { as it :Pico.Frame =>
                    if is?(it, :Pico.Frame) {
                        set pub().rect.pos.y = pub().rect.pos.y - (it.ms * 0.5)
                        (pub().rect.pos.y < -((WIN.y/2) - 45))
                    } else {
                        if is?(it, :Pico.Draw) {
                            pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png");
                            false
                        } else {
                            false
                        }
                    }
                }
            }

            spawn (task () :void {
                xloop {
                    do {
                        val awt = spawn Delay(100)
                        xloop {
                            xbreak if yield(nil) { as it => it==awt }
                        }
                    }
                    spawn (task () :void {
                        xloop {
                            yield(nil) { as it =>
                                if is?(it, :Pico.Draw) {
                                    pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png")
                                } else {
                                    nil
                                }
                            }
                        }
                    }) ()
                    do {
                        val awt = spawn Delay(100)
                        xloop {
                            xbreak if yield(nil) { as it => it==awt }
                        }
                    }
                }
            }) ()

            do {
                val awt = spawn Delay(1000)
                xloop {
                    xbreak if yield(nil) { as it => it==awt }
                }
            }
        }) ()

    xloop {
        xbreak if (pub().rect.pos.x > (WIN.x/2))
        xbreak if yield(nil) { as it => (it == Y) }
    }
}

spawn (task () {
    val game = spawn (task () {
        val birds = tasks(5)
        spawn (task () {
            xloop {
                do {
                    val awt = spawn Delay(500)
                    xloop {
                        xbreak if yield(nil) { as it => it==awt }
                    }
                }
                val y = -(WIN.y/2) + (random-next() % WIN.y)
                val s = 100 + (random-next() % 100)
                spawn Bird([y,s]) in birds
            }
        }) ()
        spawn (task () {
            xloop {
                xloop { ;; AWAIT
                    xbreak if yield(nil) { as it =>
                        is?(it, :Pico.Frame)
                    }
                }
                var b1 = nil
                xloop {
                    set b1 = next(birds, b1)
                    xbreak if b1==nil
                    var b2 = nil
                    xloop {
                        set b2 = next(birds, b2)
                        xbreak if b2==nil
                        val col? =
                            detrack(b1) { as x1 :Bird =>
                                detrack(b2) { as x2 :Bird =>
                                    (x1 /= x2)      &&
                                    (pub(x1).alive) &&
                                    (pub(x2).alive) &&
                                    pico-rect-vs-rect?(pub(x1).rect, pub(x2).rect)
                                }
                            }
                        if col? {
                            broadcast(:collided) in b1
                            broadcast(:collided) in b2
                        } else {
                            nil
                        }
                    }
                }
            }
        }) ()
        spawn (task () {
            xloop {
                val bird =
                    xloop {
                        xbreak if yield(nil) { as evt :Pico.Mouse.Button.Dn =>
                            if is?(evt, :Pico.Mouse.Button.Dn) {
                                var b = nil
                                xloop {
                                    set b = next(birds, b)
                                    xbreak(false) if b==nil
                                    xbreak(drop(b)) if detrack(b) { as x :Bird =>
                                        pico-point-vs-rect?(evt.pos, pub(x).rect)
                                    }
                                }
                            } else {
                                nil
                            }
                        }
                    }
println(:bird, bird)
                xloop {
                    xbreak if nil == yield(nil) { as it =>
                        if is?(it, :Pico.Draw) {
                            detrack(bird) { as x :Bird =>
                                pico-output-draw-line([0,-240], pub(x).rect.pos)
                                true
                            }
                        } else {
                            true
                        }
                    }
                }
            }
        }) ()
        xloop {
            yield(nil) { as it => nil }
        }
    }) ()
    xloop {
        xloop { ;; AWAIT
            xbreak if yield(nil) { as it :Pico.Key.Dn =>
                if is?(it, :Pico.Key.Dn) {
                    (it.key == :Key-P)
                } else {
                    false
                }
            }
        }
        toggle game(false)
        xloop {
            xbreak if yield(nil) { as it :Pico.Key.Dn =>
                if is?(it, :Pico.Draw) {
                    pico-output-draw-image([0,0], "res/pause.png");
                    false
                } else {
                    if is?(it, :Pico.Key.Dn) {
                        (it.key == :Key-P)
                    } else {
                        nil
                    }
                }
            }
        }
        toggle game(true)
    }
}) ()

xloop {
    val e = resume pico-coro ()
    xbreak if do {
        if e {
            broadcast(e)
            is?(e, :Pico.Quit)
        } else {
            nil
        }
    }
}
