^["pico.ceu"]

pico_state_set_title(`#pointer "Birds - 02 (scope)"`)
pico_state_set_size(640,480)
pico_state_set_size_pixel(1,1)
pico_state_set_grid(false)
pico_state_set_color_clear(0,0,0,255)

var win = pico_state_get_size()

var Bird = task (y,speed) {
    var rct = [[(-win[0]/2),y], [50,45]]
    var sin = 0
    par {
        var xx = rct[0][0]
        var yy = rct[0][1]
        while true {
            await (evt[#type] == #frame)
            var xspeed = speed
            var v = evt[#dt] * speed
            set xx = xx + (v/1000)
            set y = yy + ((xspeed/5) * `#number sin($sin.Number)`)
            set rct[0] = [xx,y]
            set sin = sin + (3.14*v/100000)
        }
    } with {
        while (true) {
            yield ()
            await (evt[#type] == #draw)
            if `#number ((int)(($sin.Number+(3.14/2))/3.14))` % 2 == 0 {
                pico_output_draw_image(rct[0][0],rct[0][1], `#pointer "res/clumsy_up.png"`);
            } else {
                pico_output_draw_image(rct[0][0],rct[0][1], `#pointer "res/clumsy_dn.png"`);
            }
        }
    }
}

spawn {
    while true {
        spawn Bird (150, 100)
        spawn Bird (  0, 200)
        await (evt[#type] == #mousebuttondown)
    }
}
pico_loop ()
