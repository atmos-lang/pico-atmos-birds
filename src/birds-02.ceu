^["pico.ceu"]

pico_state_set_title("Birds - 02 (scope)")
pico_state_set_size(640,480)
pico_state_set_size_pixel(1,1)
pico_state_set_grid(false)
pico_state_set_color_clear(0,0,0,255)

var win = pico_state_get_size()

var Bird = task (y,speed) {
    var xx = (-win[0]) / 2
    var yy = y
    var sin = 0
    par {
        every :frame {
            var v = evt.0 * speed
            set xx = xx + (v/1000)
            set yy = y + ((speed/5) * `:number sin($sin.Number)`)
            set sin = sin + (3.14*v/100000)
        }
    } with {
        every :draw {
            if `:number ((int)(($sin.Number+(3.14/2))/3.14))` % 2 == 0 {
                pico_output_draw_image(xx,yy, "res/clumsy_up.png");
            } else {
                pico_output_draw_image(xx,yy, "res/clumsy_dn.png");
            }
        }
    }
}

spawn {
    while true {
        spawn Bird (150, 100)
        spawn Bird (  0, 200)
        await :mousebuttondown
    }
}
pico_loop ()
