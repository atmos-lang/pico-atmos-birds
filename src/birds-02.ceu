^["@/pico/pico.ceu"]

pico-state-set-title("Birds - 02 (scope)")
pico-state-set-size([640,480])
pico-state-set-size-pixel([1,1])
pico-state-set-grid(false)
pico-state-set-color-clear([0,0,0,255])
pico-state-set-color-draw([255,255,255,255])

val WIN :XY = pico-state-get-size()

task Bird (y,speed) {
    var xx = (-WIN.x) / 2
    var yy = y
    var sin = 0
    par {
        every :Pico.Frame {
            val v = evt.ms * speed
            set xx = xx + (v/1000)
            set yy = y + ((speed/5) * `:number sin($sin.Number)`)
            set sin = sin + ((3.14*v)/100000)
        }
    } with {
        every :Pico.Draw {
            if (`:number ((int)(($sin.Number+(3.14/2))/3.14))` % 2) == 0 {
                pico-output-draw-image([xx,yy], "res/clumsy_up.png");
            } else {
                pico-output-draw-image([xx,yy], "res/clumsy_dn.png");
            }
        }
    }
}

spawn {
    loop {
        spawn Bird (150, 100)
        spawn Bird (  0, 200)
        await :Pico.Mouse.Button.Dn
    }
}

pico-loop ()
