^["@/pico/pico.ceu"]

val math-sin = func (v) {
    `:number sin($v.Number)`
}
val math-floor = func (v) {
    `:number floor($v.Number)`
}
val random-seed = func (n) {
    if n {
        `srand($n.Number);`
    } else {
        `srand(time(NULL));`
    }
}
val random-next = func () {
    `:number rand()`
}

pico-state-set-title        ("Birds - 11 (tracking)")
pico-state-set-size         ([640,480])
pico-state-set-size-pixel   ([1,1])
pico-state-set-grid         (false)
pico-state-set-color-clear  ([0,0,0,255])
pico-state-set-color-draw   ([255,255,255,255])

random-seed()

val WIN :XY = pico-state-get-size()

val Delay = task (ms) {
    var tot = ms
    loop {
        yield(nil)
        break if do {
            if is?(evt,:Clock) {
                set tot = tot - evt[0]
                tot <= 0
            } else {
                false
            }
        }
    }
}

data :Bird = [alive,rect:Rect]

val Bird = task (t) :Bird {
    val y = t[0]
    val speed = t[1]
    set pub() = [true, [[(-WIN.x)/2,y], [100,90]]]
    var ang = 0
    val Y =
        spawn (task () :void {
            loop {
                yield(nil)
                break if (func (it :Pico.Frame) {
                    if is?(it, :Pico.Frame) {
                        val v = it.ms * speed
                        set pub().rect.pos.x = pub().rect.pos.x + (v/1000)
                        set pub().rect.pos.y = y + ((speed/5) * math-sin(ang))
                        set ang = ang + ((3.14*v)/100000)
                        false
                    } else {
                        if is?(it, :Pico.Draw) {
                            if (math-floor(((ang+(3.14/2))/3.14)) % 2) == 0 {
                                pico-output-draw-image(pub().rect.pos, "res/clumsy_up.png");
                            } else {
                                pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png");
                            }
                            false
                        } else {
                            (it == :collided)
                        }
                    }
                }) (evt)
            }

            set pub().alive = false

            loop {
                yield(nil)
                break if (func (it :Pico.Frame) {
                    if is?(it, :Pico.Frame) {
                        set pub().rect.pos.y = pub().rect.pos.y - (it.ms * 0.5)
                        (pub().rect.pos.y < -((WIN.y/2) - 45))
                    } else {
                        if is?(it, :Pico.Draw) {
                            pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png");
                            false
                        } else {
                            false
                        }
                    }
                }) (evt)
            }

            spawn (task () :void {
                loop {
                    do {
                        val awt = spawn Delay(100)
                        loop {
                            yield(nil)
                            break if evt==awt
                        }
                    }
                    spawn (task () :void {
                        loop {
                            yield(nil)
                            if is?(evt, :Pico.Draw) {
                                pico-output-draw-image(pub().rect.pos, "res/clumsy_dn.png")
                            } else {
                                nil
                            }
                        }
                    }) ()
                    do {
                        val awt = spawn Delay(100)
                        loop {
                            yield(nil)
                            break if evt==awt
                        }
                    }
                }
            }) ()

            do {
                val awt = spawn Delay(1000)
                loop {
                    yield(nil)
                    break if evt==awt
                }
            }
        }) ()

    loop {
        break if (pub().rect.pos.x > (WIN.x/2))
        yield(nil)
        break if evt==Y
    }
}

spawn (task () {
    val game = spawn (task () {
        val birds = tasks(5)
        spawn (task () {
            loop {
                do {
                    val awt = spawn Delay(500)
                    loop {
                        yield(nil)
                        break if evt==awt
                    }
                }
                val y = -(WIN.y/2) + (random-next() % WIN.y)
                val s = 100 + (random-next() % 100)
println(:spawn)
                spawn Bird([y,s]) in birds
            }
        }) ()
        spawn (task () {
            loop {
                loop { ;; AWAIT
                    yield(nil)
                    break if is?(evt, :Pico.Frame)
                }
                var b1 = nil
                loop {
                    set b1 = next-tasks(birds, b1)
                    break if b1==nil
                    var b2 = nil
                    loop {
                        set b2 = next-tasks(birds, b2)
                        break if b2==nil
                        val col? =
                            detrack(b1) { x1 :Bird =>
                                detrack(b2) { x2 :Bird =>
                                    (x1 /= x2)      &&
                                    (pub(x1).alive) &&
                                    (pub(x2).alive) &&
                                    pico-rect-vs-rect?(pub(x1).rect, pub(x2).rect)
                                }
                            }
                        if col? {
                            detrack(b1) { x1 => broadcast(:collided) in x1 }
                            detrack(b2) { x2 => broadcast(:collided) in x2 }
                        } else {
                            nil
                        }
                    }
                }
            }
        }) ()
        spawn (task () {
            loop {
                val bird =
                    loop {
                        yield(nil)
                        break if (func (it :Pico.Mouse.Button.Dn) {
                            if is?(it, :Pico.Mouse.Button.Dn) {
                                var b = nil
                                loop {
                                    set b = next-tasks(birds, b)
                                    break(false) if b==nil
                                    break(drop(b)) if detrack(b) { x :Bird =>
                                        pico-point-vs-rect?(it.pos, pub(x).rect)
                                    }
                                }
                            } else {
                                nil
                            }
                        }) (evt)
                    }
println(:bird, bird)
                loop {
                    yield(nil)
                    break if nil == do {
                        if is?(evt, :Pico.Draw) {
                            detrack(bird) { x :Bird =>
                                if x {
                                    pico-output-draw-line([0,-240], pub(x).rect.pos)
                                    true
                                } else {
                                    nil
                                }
                            }
                        } else {
                            true
                        }
                    }
                }
            }
        }) ()
        loop {
            yield(nil)
        }
    }) ()
    loop {
        loop { ;; AWAIT
            yield(nil)
            break if (func (it :Pico.Key.Dn) {
                if is?(it, :Pico.Key.Dn) {
                    (it.key == :Key-P)
                } else {
                    false
                }
            }) (evt)
        }
        toggle game(false)
        loop {
            yield(nil)
            break if (func (it :Pico.Key.Dn) {
                if is?(it, :Pico.Draw) {
                    pico-output-draw-image([0,0], "res/pause.png");
                    false
                } else {
                    if is?(it, :Pico.Key.Dn) {
                        (it.key == :Key-P)
                    } else {
                        nil
                    }
                }
            }) (evt)
        }
        toggle game(true)
    }
}) ()

pico-loop()
