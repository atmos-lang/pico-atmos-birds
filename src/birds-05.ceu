^["@/pico/pico.ceu"]

pico.set.title       <- "Birds - 05 (termination)"
pico.set.size        <- [640,480]
pico.set.grid        <- false
pico.set.color.clear <- [0,0,0,255]
pico.set.color.draw  <- [255,255,255,255]

random.seed()

task Bird (y, speed) :Rect {
    var xx = 0
    var yy = y
    var ang = 0
    watching |(xx > 640) {
        par {
            every :Pico.Frame {
                val v = it.ms * speed
                set xx = xx + (v/1000)
                set yy = y - ((speed/5) * math.sin(ang))
                set ang = ang + ((3.14*v)/100000)
            }
        } with {
            every :Pico.Draw {
                if (math.floor(((ang+(3.14/2))/3.14)) % 2) == 0
                    => pico.output.draw.image([xx,yy], "res/clumsy_up.png");
                    => pico.output.draw.image([xx,yy], "res/clumsy_dn.png");
            }
        }
    }
}

spawn {
    val birds = tasks(5)
    every <500:ms> {
        (spawn Bird(y,s) in birds) where {
            val y = random.next() % 480
            val s = 100 + (random.next() % 100)
        }
    }
}

pico.loop()
