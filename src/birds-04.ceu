^["pico.ceu"]

pico_state_set_title(`:pointer "Birds - 04 (bounded)"`)
pico_state_set_size(640,480)
pico_state_set_size_pixel(1,1)
pico_state_set_grid(false)
pico_state_set_color_clear(0,0,0,255)

var win = pico_state_get_size()

task Bird (y,speed) {
    var xx = (-win[0]) / 2
    var yy = y
    var sin = 0
    par {
        every evt[:type]==:frame {
            var v = evt[:dt] * speed
            set xx = xx + (v/1000)
            set yy = y + ((speed/5) * `:number sin($sin.Number)`)
            set sin = sin + (3.14*v/100000)
        }
    } with {
        every evt[:type]==:draw {
            if `:number ((int)(($sin.Number+(3.14/2))/3.14))` % 2 == 0 {
                pico_output_draw_image(xx,yy, `:pointer "res/clumsy_up.png"`);
            } else {
                pico_output_draw_image(xx,yy, `:pointer "res/clumsy_dn.png"`);
            }
        }
    }
}

spawn {
    var birds = coroutines(5)
    every 500ms {
        (spawn in birds, Bird(y,s)) where {
            var y = (`:number rand()` % 480) - 240
            var s = 100 + (`:number rand()` % 100)
        }
    }
}

pico_loop ()
